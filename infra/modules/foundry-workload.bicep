param location string
param foundryAccountName string
param resolvedFoundryProjectName string
param modelDeploymentName string
param modelDeploymentSkuName string
param modelDeploymentCapacity int
param cohereModelName string
param cohereModelVersion string
param embedDeploymentName string
param embedModelName string
param embedModelVersion string
param embedDeploymentCapacity int
param rerankDeploymentName string
param rerankModelName string
param rerankModelVersion string
param rerankDeploymentCapacity int

resource foundryAccount 'Microsoft.CognitiveServices/accounts@2025-06-01' existing = {
  name: foundryAccountName
}

resource foundryProject 'Microsoft.CognitiveServices/accounts/projects@2025-06-01' = {
  parent: foundryAccount
  name: resolvedFoundryProjectName
  location: location
  identity: {
    type: 'SystemAssigned'
  }
  properties: {}
}

resource modelDeployment 'Microsoft.CognitiveServices/accounts/deployments@2025-06-01' = {
  parent: foundryAccount
  name: modelDeploymentName
  sku: {
    name: modelDeploymentSkuName
    capacity: modelDeploymentCapacity
  }
  properties: {
    model: {
      format: 'Cohere'
      name: cohereModelName
      version: cohereModelVersion
    }
  }
  dependsOn: [
    foundryProject
  ]
}

resource embedModelDeployment 'Microsoft.CognitiveServices/accounts/deployments@2025-06-01' = {
  parent: foundryAccount
  name: embedDeploymentName
  sku: {
    name: 'GlobalStandard'
    capacity: embedDeploymentCapacity
  }
  properties: {
    model: {
      format: 'Cohere'
      name: embedModelName
      version: embedModelVersion
    }
  }
  dependsOn: [
    foundryProject
    modelDeployment
  ]
}

resource rerankModelDeployment 'Microsoft.CognitiveServices/accounts/deployments@2025-06-01' = {
  parent: foundryAccount
  name: rerankDeploymentName
  sku: {
    name: 'GlobalStandard'
    capacity: rerankDeploymentCapacity
  }
  properties: {
    model: {
      format: 'Cohere'
      name: rerankModelName
      version: rerankModelVersion
    }
  }
  dependsOn: [
    foundryProject
    embedModelDeployment
  ]
}

var foundryEndpointBase = 'https://${foundryAccount.name}.services.ai.azure.com'
var embedEndpoint = '${foundryEndpointBase}/openai/deployments/${embedDeploymentName}'
var rerankEndpoint = foundryEndpointBase
var foundryProjectEndpoint = '${foundryEndpointBase}/api/projects/${foundryProject.name}'

output foundryProjectName string = foundryProject.name
output foundryProjectResourceId string = foundryProject.id
output foundryProjectPrincipalId string = foundryProject.identity.principalId
output foundryProjectEndpoint string = foundryProjectEndpoint
output embedEndpoint string = embedEndpoint
output rerankEndpoint string = rerankEndpoint
output modelDeploymentName string = modelDeploymentName
output embedDeploymentName string = embedDeploymentName
output embedModelName string = embedModelName
output rerankDeploymentName string = rerankDeploymentName
output rerankModelName string = rerankModelName
