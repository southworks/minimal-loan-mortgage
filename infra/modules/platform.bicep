param location string
param resourceTags object
param logAnalyticsName string
param containerAppsEnvironmentName string

var applicationInsightsName = take(replace('${logAnalyticsName}-appi', '-logs-', '-'), 260)

resource logAnalytics 'Microsoft.OperationalInsights/workspaces@2023-09-01' = {
  name: logAnalyticsName
  location: location
  tags: resourceTags
  properties: {
    sku: {
      name: 'PerGB2018'
    }
    retentionInDays: 30
  }
}

resource applicationInsights 'Microsoft.Insights/components@2020-02-02' = {
  name: applicationInsightsName
  location: location
  tags: resourceTags
  kind: 'web'
  properties: {
    Application_Type: 'web'
    WorkspaceResourceId: logAnalytics.id
  }
}

resource containerAppsEnvironment 'Microsoft.App/managedEnvironments@2024-03-01' = {
  name: containerAppsEnvironmentName
  location: location
  tags: resourceTags
  properties: {
    appLogsConfiguration: {
      destination: 'log-analytics'
      logAnalyticsConfiguration: {
        customerId: logAnalytics.properties.customerId
        sharedKey: logAnalytics.listKeys().primarySharedKey
      }
    }
  }
}

output containerAppsEnvironmentId string = containerAppsEnvironment.id
output logAnalyticsCustomerId string = logAnalytics.properties.customerId
output applicationInsightsName string = applicationInsights.name
output applicationInsightsResourceId string = applicationInsights.id
@secure()
output applicationInsightsConnectionString string = applicationInsights.properties.ConnectionString
