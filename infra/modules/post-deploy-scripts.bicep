param location string
param resourceTags object
param deploymentSuffix string
param nameSuffix string
param deploymentScriptIdentityName string
param foundryAccountName string
param foundryProjectName string
param policySeedJobName string
param provisioningJobName string
param enableFabricSeed bool
param fabricSeedTimeout string
param fabricUamiResourceId string
param fabricUamiClientId string
param fabricWorkspaceId string
param fabricWorkspaceName string
param fabricLakehouseId string
param fabricLakehouseName string
param fabricRepositoryArchiveUrl string
@secure()
param fabricGithubToken string
param fabricSkipRaw bool
param fabricSkipStructured bool
param fabricSkipPolicy bool

resource fabricUami 'Microsoft.ManagedIdentity/userAssignedIdentities@2023-01-31' existing = {
  name: last(split(fabricUamiResourceId, '/'))
}

resource foundryAccount 'Microsoft.CognitiveServices/accounts@2025-06-01' existing = {
  name: foundryAccountName
}

resource foundryProject 'Microsoft.CognitiveServices/accounts/projects@2025-06-01' existing = {
  parent: foundryAccount
  name: foundryProjectName
}

resource deploymentScriptIdentity 'Microsoft.ManagedIdentity/userAssignedIdentities@2023-01-31' = {
  name: deploymentScriptIdentityName
  location: location
  tags: resourceTags
}

resource deploymentScriptContributorRole 'Microsoft.Authorization/roleAssignments@2022-04-01' = {
  name: guid(resourceGroup().id, deploymentScriptIdentity.id, 'Contributor', nameSuffix)
  scope: resourceGroup()
  properties: {
    roleDefinitionId: subscriptionResourceId('Microsoft.Authorization/roleDefinitions', 'b24988ac-6180-42a0-ab88-20f7382dd24c')
    principalId: deploymentScriptIdentity.properties.principalId
    principalType: 'ServicePrincipal'
  }
}

resource deploymentScriptFoundryContributorRole 'Microsoft.Authorization/roleAssignments@2022-04-01' = {
  name: guid(foundryAccount.id, deploymentScriptIdentity.id, 'CognitiveServicesContributor', nameSuffix)
  scope: foundryAccount
  properties: {
    roleDefinitionId: subscriptionResourceId('Microsoft.Authorization/roleDefinitions', '25fbc0a9-bd7c-42a3-aa1a-3b75d497ee68')
    principalId: deploymentScriptIdentity.properties.principalId
    principalType: 'ServicePrincipal'
  }
  dependsOn: [
    foundryProject
  ]
}

resource deploymentScriptFoundryUserRole 'Microsoft.Authorization/roleAssignments@2022-04-01' = {
  name: guid(foundryAccount.id, deploymentScriptIdentity.id, 'FoundryUser', nameSuffix)
  scope: foundryAccount
  properties: {
    roleDefinitionId: subscriptionResourceId('Microsoft.Authorization/roleDefinitions', '53ca6127-db72-4b80-b1b0-d745d6d5456d')
    principalId: deploymentScriptIdentity.properties.principalId
    principalType: 'ServicePrincipal'
  }
  dependsOn: [
    foundryProject
  ]
}

resource runPolicySeedScript 'Microsoft.Resources/deploymentScripts@2023-08-01' = {
  name: 'run-policy-seed-${deploymentSuffix}'
  location: location
  tags: resourceTags
  kind: 'AzureCLI'
  identity: {
    type: 'UserAssigned'
    userAssignedIdentities: {
      '${deploymentScriptIdentity.id}': {}
    }
  }
  properties: {
    azCliVersion: '2.62.0'
    timeout: 'PT45M'
    retentionInterval: 'PT1H'
    cleanupPreference: 'OnSuccess'
    forceUpdateTag: deploymentSuffix
    scriptContent: '''
      set -euo pipefail
      az extension add --name containerapp --upgrade 2>/dev/null || true
      echo "Waiting for role assignments and Foundry deployments to settle..."
      sleep 180
      echo "Starting policy seed job..."
      EXECUTION=$(az containerapp job start --name "${POLICY_SEED_JOB_NAME}" --resource-group "${RESOURCE_GROUP}" --query name -o tsv)
      echo "Policy seed job execution: ${EXECUTION}"

      for i in $(seq 1 120); do
        STATUS=$(az containerapp job execution show \
          --name "${POLICY_SEED_JOB_NAME}" \
          --resource-group "${RESOURCE_GROUP}" \
          --job-execution-name "${EXECUTION}" \
          --query properties.status -o tsv)

        echo "Policy seed job status: ${STATUS}"

        if [ "${STATUS}" = "Succeeded" ]; then
          echo "Policy seeding completed successfully."
          exit 0
        fi

        if [ "${STATUS}" = "Failed" ]; then
          echo "Policy seed job failed. Fetching recent job logs..."
          az containerapp job logs show \
            --name "${POLICY_SEED_JOB_NAME}" \
            --resource-group "${RESOURCE_GROUP}" \
            --execution "${EXECUTION}" \
            --container policy-seed \
            --tail 50 || true
          exit 1
        fi

        sleep 15
      done

      echo "Timed out waiting for policy seed job."
      exit 1
    '''
    environmentVariables: [
      {
        name: 'RESOURCE_GROUP'
        value: resourceGroup().name
      }
      {
        name: 'POLICY_SEED_JOB_NAME'
        value: policySeedJobName
      }
    ]
  }
  dependsOn: [
    deploymentScriptContributorRole
  ]
}

resource runProvisioningScript 'Microsoft.Resources/deploymentScripts@2023-08-01' = {
  name: 'run-agent-provisioning-${deploymentSuffix}'
  location: location
  tags: resourceTags
  kind: 'AzureCLI'
  identity: {
    type: 'UserAssigned'
    userAssignedIdentities: {
      '${deploymentScriptIdentity.id}': {}
    }
  }
  properties: {
    azCliVersion: '2.62.0'
    timeout: 'PT45M'
    retentionInterval: 'PT1H'
    cleanupPreference: 'OnSuccess'
    forceUpdateTag: deploymentSuffix
    scriptContent: '''
      set -euo pipefail
      az extension add --name containerapp --upgrade 2>/dev/null || true
      echo "Waiting briefly for role assignment propagation..."
      sleep 60
      echo "Starting agent provisioning job..."
      EXECUTION=$(az containerapp job start --name "${PROVISIONING_JOB_NAME}" --resource-group "${RESOURCE_GROUP}" --query name -o tsv)
      echo "Job execution: ${EXECUTION}"

      for i in $(seq 1 120); do
        STATUS=$(az containerapp job execution show \
          --name "${PROVISIONING_JOB_NAME}" \
          --resource-group "${RESOURCE_GROUP}" \
          --job-execution-name "${EXECUTION}" \
          --query properties.status -o tsv)

        echo "Provisioning job status: ${STATUS}"

        if [ "${STATUS}" = "Succeeded" ]; then
          echo "Agent provisioning completed successfully."
          exit 0
        fi

        if [ "${STATUS}" = "Failed" ]; then
          echo "Agent provisioning job failed."
          az containerapp job logs show \
            --name "${PROVISIONING_JOB_NAME}" \
            --resource-group "${RESOURCE_GROUP}" \
            --execution "${EXECUTION}" \
            --container agent-provisioning \
            --tail 50 || true
          exit 1
        fi

        sleep 15
      done

      echo "Timed out waiting for agent provisioning job."
      exit 1
    '''
    environmentVariables: [
      {
        name: 'RESOURCE_GROUP'
        value: resourceGroup().name
      }
      {
        name: 'PROVISIONING_JOB_NAME'
        value: provisioningJobName
      }
    ]
  }
  dependsOn: [
    deploymentScriptContributorRole
    runPolicySeedScript
  ]
}

resource runFabricSeed 'Microsoft.Resources/deploymentScripts@2023-08-01' = if (enableFabricSeed) {
  name: 'run-fabric-seed-${deploymentSuffix}'
  location: location
  tags: resourceTags
  kind: 'AzurePowerShell'
  identity: {
    type: 'UserAssigned'
    userAssignedIdentities: {
      '${fabricUami.id}': {}
    }
  }
  properties: {
    azPowerShellVersion: '11.0'
    retentionInterval: 'P1D'
    timeout: fabricSeedTimeout
    cleanupPreference: 'OnSuccess'
    forceUpdateTag: deploymentSuffix
    scriptContent: loadTextContent('../scripts/seed-fabric-data.ps1')
    environmentVariables: [
      { name: 'AZURE_CLIENT_ID',         value: fabricUamiClientId }
      { name: 'FABRIC_WORKSPACE_ID',     value: fabricWorkspaceId }
      { name: 'FABRIC_WORKSPACE_NAME',   value: fabricWorkspaceName }
      { name: 'FABRIC_LAKEHOUSE_ID',     value: fabricLakehouseId }
      { name: 'FABRIC_LAKEHOUSE_NAME',   value: fabricLakehouseName }
      { name: 'RESOURCE_GROUP_NAME',     value: resourceGroup().name }
      { name: 'REPOSITORY_ARCHIVE_URL',  value: fabricRepositoryArchiveUrl }
      { name: 'GITHUB_TOKEN',            secureValue: fabricGithubToken }
      { name: 'SKIP_RAW',                value: string(fabricSkipRaw) }
      { name: 'SKIP_STRUCTURED',         value: string(fabricSkipStructured) }
      { name: 'SKIP_POLICY',             value: string(fabricSkipPolicy) }
    ]
  }
  dependsOn: [
  ]
}

output fabricSeedDeploymentScriptName string = enableFabricSeed ? runFabricSeed.name : ''
